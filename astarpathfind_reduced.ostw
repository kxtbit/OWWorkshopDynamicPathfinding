/*
 * Overwatch Workshop Dynamic Pathfinding
 * made by kxtbit#1903 (battle.net / workshop.codes)
 * originally uploaded at https://github.com/kxtbit/OWWorkshopDynamicPathfinding/blob/main/astarpathfind_reduced.ostw
 * please credit me if you use this for your stuff, also LGPLv3 and stuff cool bye
 */

//macro that checks line of sight between cells
//uses RayCastHitPosition instead of IsInLineOfSight because there was some weird inaccuracies with the latter
Boolean pathfindLOSCheck(Vector pos, Vector dir): RayCastHitPosition(pos, pos + dir, null, null, false) == pos + dir;
//line of sight check for the overlayed grid thingy
Boolean flatLOSCheck(Vector pos, Vector dir):
    pathfindLOSCheck(pos, dir + Vector(0, 0.25, 0));
//macro to get the correct grid position for a neighbor using the overlayed grid thingy
Vector moveGridPos(Vector rootPos, Vector pos, Vector dir):
    RayCastHitPosition(pos + dir + Vector(0, 0.25, 0), pos + dir + Vector(0, -9999, 0), null, null, false) - rootPos + Vector(0, 1, 0);

//gets the neighbor nodes of a node
Any[][] getNeighbors(Any[] node, Vector firstPos, Vector lastPos) {
    Any[][] neighbors = [];
    Vector rootPos = node[0];
    Vector gridPos = node[1];
    Vector pos = rootPos + gridPos;
    foreach (Vector dir in [Forward(), Right(), Backward(), Left(), Backward() + Left(), Left() + Forward(), Forward() + Right(), Right() + Backward()]) {
        if (flatLOSCheck(pos, dir * 1.5)) {
            neighbors.ModAppend([[rootPos, moveGridPos(rootPos, pos, dir * 1.5), 1]]);
        }
    }

    return neighbors;
}
//gets the neighbor nodes of a node, with experimental optimizations
Any[][] getNeighborsExperimental(Any[] node, Vector firstPos, Vector lastPos) {
    Any[][] neighbors = [];
    Vector rootPos = node[0];
    Vector gridPos = node[1];
    Vector pos = rootPos + gridPos;
    foreach (Vector dir in [Forward(), Right(), Backward(), Left(), Backward() + Left(), Left() + Forward(), Forward() + Right(), Right() + Backward()]) {
        if (flatLOSCheck(pos, dir * 1.5)) {
            neighbors.ModAppend([[rootPos, moveGridPos(rootPos, pos, dir * 1.5), 1]]);
        }
        if (/*DistanceBetween(firstPos, lastPos) > 75 && */DistanceBetween(pos, lastPos) > 50) {
            Number i = 2;
            while (flatLOSCheck(pos, dir * (1.5 * i)) && i < 5) {
                if (RayCastHitPosition(pos + dir * (1.5 * i), pos + dir * (1.5 * i) + Vector(0, -2, 0), null, null, true).Y 
                <= RayCastHitPosition(pos + dir * (1.5 * (i - 1)), pos + dir * (1.5 * (i - 1)) + Vector(0, -2, 0), null, null, true).Y + 0.75)
                    neighbors.ModAppend([[rootPos, moveGridPos(rootPos, pos, dir * (1.5 * i)), i]]);
                else
                    break;
                i += 2;
                antiCrashCounter += 1;
            }
        }
    }

    return neighbors;
}


//adds a node to a priority queue
void priorityQueueAdd(ref Any[][] list, ref Any[] obj, ref Boolean listSorted) {
    list.ModAppend([obj]);
    listSorted = false;
}
globalvar Number antiCrashCounter = 0;
//removes the lowest priority node from a priority queue
Any[] priorityQueueRemove(ref Any[][] list, Number waitThreshold, ref Boolean listSorted) {
    if (antiCrashCounter >= waitThreshold) {
        Wait(0.016);
        antiCrashCounter = 0;
    }
    //if the list is already sorted, don't sort it again
    if (!listSorted) {
        list = SortedArray(list, ArrayElement()[2]);
        listSorted = true;
    }
    antiCrashCounter += list.Length * 0.08;
    Any[] value = list[0];
    list.ModRemoveByIndex(0);
    return value;
}

//gets the dual index for a key in a dual index dictionary
Number[] getDualIndex(ref Any[][] dArray, ref Any value) {
    Number[] mapped = MappedArray(dArray, IndexOfArrayValue(ArrayElement(), value));
    Number[] ret;
    for (Number i = 0; i < mapped.Length; i++) {
        if (mapped[i] != -1) {
            ret = [i, mapped[i]];
            break;
        }
    }
    if (ret == null) ret = [];
    return ret;
}

//stores references to all debug effects
globalvar Any[] vizEffects = [];
//pathfinding heuristic function
Number heuristic(Vector lastPos, Vector firstPos, Vector pos): DistanceBetween(lastPos, pos) * 1.005;

//the pathfinder itself. firstPos is the starting position, and lastPos is the goal position.
//if refinePath is off, it disables the path refiner, which causes more steps to be in the resulting path
//and also may make it longer, but can fix some issues with certain maps
//nodeLimit sets the amount of nodes generated that will cause the algorithm to fail
//(to prevent pathfinding from taking too long or crashing the server)
//forceAlgorithm changes the pathfinding algorithm used. 0 is automatic, 1 is A*, and 2 is Greedy Best First Search
//waitThreshhold controls when the algorithm will wait to reduce server load. on the default setting of 10, it will be fairly fast
//but will probably also max out or close to max out the server lode when pathfinding. values above 15 are in danger of crashing the server quickly,
//and values below 5 will cause the algorithm to be slow.

//the function returns a list of vectors containing each point in the path that was found, or an empty array if no path was found.
public Vector[] pathfind(Vector firstPos, Vector lastPos, Boolean refinePath = true, Number nodeLimit = 1500, Number forceAlgorithm = 0, Number waitThreshold = 10, Boolean useExperimentalOptimizations = true) {
    //uncomment this to enable debug visualization (2 more below)
    //DestroyEffect(vizEffects);
    //vizEffects = [];
    Boolean listSorted = false;
    Boolean useAStar = forceAlgorithm == 0 ? (DistanceBetween(firstPos, lastPos) <= 75) : (forceAlgorithm == 1);

    Any[][] frontier = [];
    Any[] first = [firstPos, Vector(0, 0, 0), 0];
    Vector[] valKeys = [first[1]];
    Number[] costsValues = [0];
    Any[][] parentsValues = [null];
    antiCrashCounter = 0;

    Boolean useDualIndex = false;

    //some variable stuffs
    Number i1;
    Number i2;
    Boolean check;
    Number newCost;
    Number[] dualIndex;
    Boolean costCheck;
    Number[] dualIndex2;

    priorityQueueAdd(frontier, first, listSorted);
    while (frontier.Length != 0) {
        //emergency anti-crash wait
        if (ServerLoad() >= 255) {
            Wait(0.2);
        }
        Any[] current = priorityQueueRemove(frontier, waitThreshold, listSorted);
        Number i;
        if (nodeLimit != -1) {
            if (dualIndex) {
                Number length = 0;
                for (i = 0; valKeys.Length; i++) {
                    length += CountOf(valKeys[i]);
                }
                if (length > nodeLimit) return [];
            } else {
                if (valKeys.Length > nodeLimit) return [];
            }
        }
        if (DistanceBetween(current[0] + current[1], lastPos) <= 1.5 && IsInLineOfSight(lastPos, current[0] + current[1])) {
            Vector[] finalPath = [lastPos];
            Any[] node = current;
            while (node != null) {
                finalPath = InsertValueInArray(finalPath, 0, node[0] + node[1]);
                if (useDualIndex) {
                    define di = getDualIndex(valKeys, node[1]);
                    node = parentsValues[di[0]][di[1]];
                } else
                    node = parentsValues[valKeys.IndexOf(node[1])];
            }
            Vector[] finalPathFirst = finalPath;
            Vector[] finalPathRefined;
            if (refinePath) {
                finalPathRefined = [finalPath[0]];
                Boolean couldRefine;
                while (true) {
                    couldRefine = false;
                    for (i = 0; finalPath.Length - 1; 1) {
                        Vector pos = finalPath[i];
                        Vector checkPos = finalPath[i + 2];
                        if (finalPathRefined[finalPathRefined.Length - 1] != pos)
                            finalPathRefined.ModAppend(pos);
                        if (i + 2 < finalPath.Length && RayCastHitPosition(pos, checkPos, null, null, false) == checkPos && RayCastHitPosition(checkPos, pos, null, null, false) == pos/* && checkPos.Y - pos.Y < 1*/) {
                            Boolean noHole = true;
                            for (Number v = 0; 8; 1) {
                                Vector lerpPos = LinearInterpolate(pos, checkPos, v / 5);
                                if (RayCastHitPosition(lerpPos, lerpPos + Vector(0, -1.25, 0), null, null, false) == lerpPos + Vector(0, -1.25, 0)) {
                                    noHole = false;
                                    break;
                                }
                            }
                            if (noHole) {
                                couldRefine = true;
                                finalPathRefined.ModAppend(checkPos);
                                i++; //will be incremented in the next loop
                            } else {
                                finalPathRefined.ModAppend(finalPath[i + 1]);
                            }
                        } else {
                            finalPathRefined.ModAppend(finalPath[i + 1]);
                        }
                    }
                    if (couldRefine) {
                        finalPath = finalPathRefined;
                        finalPathRefined = [finalPath[0]];
                    } else break;
                }
            } else {
                finalPathRefined = finalPath;
            }
            return finalPathRefined;
        }
        Any[][] neighbors;
        if (useExperimentalOptimizations)
            neighbors = getNeighborsExperimental(current, firstPos, lastPos);
        else
            neighbors = getNeighbors(current, firstPos, lastPos);

        for (i = 0; neighbors.Length; 1) {
            Any[] next = neighbors[i];
            if (useDualIndex) {
                define curDualIndex = getDualIndex(valKeys, current[1]);
                newCost = costsValues[curDualIndex[0]][curDualIndex[1]] + next[2];
                dualIndex = getDualIndex(valKeys, next[1]);
                check = dualIndex.Length == 0;
            } else
                check = !valKeys.Contains(next[1]);
            if (check) {
                if (useDualIndex) {
                    i1 = valKeys.Length - 1;
                    if (CountOf(valKeys[i1]) == 1000) {
                        i1++;
                    }
                    i2 = CountOf(valKeys[i1]);
                    valKeys[i1][i2] = next[1];
                    costsValues[i1][i2] = newCost;
                    parentsValues[i1][i2] = current;
                } else {
                    if (valKeys.Length == 1000) {
                        useDualIndex = true;
                        valKeys = [valKeys];
                        costsValues = [costsValues];
                        parentsValues = [parentsValues];
                        i1 = 1;
                        i2 = 0;
                        valKeys[i1][i2] = next[1];
                        costsValues[i1][i2] = newCost;
                        parentsValues[i1][i2] = current;
                    } else {
                        i1 = valKeys.Length;
                        valKeys[i1] = next[1];
                        costsValues[i1] = newCost;
                        parentsValues[i1] = current;
                    }
                }
                if (useAStar)
                    next[2] = newCost + heuristic(lastPos, firstPos, next[0] + next[1]);
                else
                    next[2] = heuristic(lastPos, firstPos, next[0] + next[1]);
                priorityQueueAdd(frontier, next, listSorted);
                //uncomment this to enable debug visualization (1 more below)
                // CreateEffect(AllPlayers(), Effect.Sphere, Color.Orange, next[0] + next[1], 0.25, EffectRev.None);
                // vizEffects.ModAppend(LastCreatedEntity());
                // if (vizEffects.Length > 150) {
                //     DestroyEffect(vizEffects[0]);
                //     vizEffects.ModRemoveByIndex(0);
                // }
            } else {
                if (useDualIndex) {
                    dualIndex2 = getDualIndex(valKeys, next[1]);
                    i1 = dualIndex2[0];
                    i2 = dualIndex2[1];
                    costCheck = newCost < costsValues[i1][i2];
                } else {
                    i1 = valKeys.IndexOf(next[1]);
                    costCheck = newCost < costsValues[i1];
                }
                if (costCheck) {
                    if (useDualIndex) {
                        costsValues[i1][i2] = newCost;
                        parentsValues[i1][i2] = current;
                    } else {
                        costsValues[i1] = newCost;
                        parentsValues[i1] = current;
                    }
                    if (useAStar)
                        next[2] = newCost + heuristic(lastPos, firstPos, next[0] + next[1]);
                    else
                        next[2] = heuristic(lastPos, firstPos, next[0] + next[1]);
                    priorityQueueAdd(frontier, next, listSorted);
                    //uncomment this to enable debug visualization
                    // CreateEffect(AllPlayers(), Effect.Sphere, Color.Orange, next[0] + next[1], 0.25, EffectRev.None);
                    // vizEffects.ModAppend(LastCreatedEntity());
                    // if (vizEffects.Length > 150) {
                    //     DestroyEffect(vizEffects[0]);
                    //     vizEffects.ModRemoveByIndex(0);
                    // }
                }
            }
            if (useDualIndex)
                antiCrashCounter += 8;
        }
    }
    return null;
}
